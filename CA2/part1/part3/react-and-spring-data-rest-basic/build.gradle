plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3' apply true
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.siouan.frontend-jdk21' version '10.0.0'
	id 'war'
}

group = 'com.greglturnquist'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

frontend {
	nodeVersion = "18.17.1"
	assembleScript = "run build"
	checkScript = "run check"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

tasks.named('test') {
	useJUnitPlatform()
}

task copyWar(type: Copy) {
	dependsOn bootWar
	from bootWar.outputs
	into file("dist")
}

task cleanWebpack(type: Delete) {
	delete 'src/main/resources/static/built'
}
clean.dependsOn cleanWebpack

bootJar {
	enabled = false
}

bootWar {
	enabled = true
	archiveFileName = 'basic-0.0.1-SNAPSHOT.war'
}

tasks {
	processResources {
		dependsOn assembleFrontend
		from("${buildDir}/frontend") {
			into 'static'
		}
	}
}